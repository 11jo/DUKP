IF
	See(Nearestenemyof(Myself))
	HPLT(Myself,110)
	HPGT(Myself,1)
	Global("ChainContingency","LOCALS",0)
THEN
	RESPONSE #100

		ReallyForceSpell(LastAttackerOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
		ReallyForceSpell(LastAttackerOf(Myself),WIZARD_INCENDIARY_CLOUD)
		ReallyForceSpell(LastAttackerOf(Myself),WIZARD_INCENDIARY_CLOUD)
		DisplayString(Myself,@210641)
		DisplayString(Myself,@210642)
		SetGlobal("ChainContingency","LOCALS",1)
END

IF
	See(Nearestenemyof(Myself))
	Global("SpellSequencer","LOCALS",0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
		DisplayString(Myself,@210643)
		SetGlobal("SpellSequencer","LOCALS",1)
END

IF
	Global("SpellTrigger","LOCALS",0)
	Range(Nearestenemyof(Myself),8)
	HPGT(Nearestenemyof(Myself),50)
	CheckStatLT(Nearestenemyof(Myself),31,RESISTFIRE)
	CheckStatLT(Nearestenemyof(Myself),31,RESISTMAGICFIRE)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_SUN_FIRE)
		ReallyForceSpell(Myself,WIZARD_SUN_FIRE)
		ReallyForceSpell(Myself,WIZARD_SUN_FIRE)
		DisplayString(Myself,@210644)
		SetGlobal("SpellTrigger","LOCALS",1)
END

IF
	See([GOODCUTOFF.GENERAL_ITEM.0.0.0.SUMMONED])
	Allegiance(LastSeenBy(Myself),ENEMY)
	LevelGT(LastSeenBy(Myself),7)
	HaveSpell(WIZARD_DEATH_SPELL)
THEN
	RESPONSE #100
		FaceObject(Lastseenby(Myself))
		Spell(Lastseenby(Myself),WIZARD_DEATH_SPELL)
END

IF
	OR(6)
		StateCheck(NearestEnemyOf(Myself),STATE_INVISIBLE)
		StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
		StateCheck(SecondNearestEnemyOf(Myself),STATE_INVISIBLE)
		StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
		StateCheck(ThirdNearestEnemyOf(Myself),STATE_INVISIBLE)
		StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	HaveSpell(WIZARD_TRUE_SIGHT)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_TRUE_SIGHT)
END

IF
	HPLT(Myself,55)
	HPGT(Myself,1)
	Global("Contingency","LOCALS",0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		DisplayString(Myself,@210645)
		SetGlobal("Contingency","LOCALS",1)
END

IF
	See(Nearestenemyof(Myself))
	HaveSpell(WIZARD_LOWER_RESISTANCE)
	CheckStatGT(LastSeenBy(Myself),40,RESISTMAGIC)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_LOWER_RESISTANCE)
END

IF
	HaveSpell(WIZARD_TIME_STOP)
	LevelGT(Nearestenemyof(Myself),9)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_TIME_STOP)
END

IF
	HaveSpell(WIZARD_GREATER_MALISON)
	OR(2)
		LevelGT(NearestEnemyOf(Myself),11)
		See(ThirdNearestEnemyOf(Myself))
	RandomNum(2,1)
THEN
	RESPONSE #100
		Spell(NearestEnemyOf(Myself),WIZARD_GREATER_MALISON)
END

IF
	See(Nearestenemyof(Myself))
	OR(2)
		Class(LastSeenBy(Myself),MAGE)
		Class(LastSeenBy(Myself),SORCERER)
	General(LastSeenBy(Myself),HUMANOID)
	HaveSpell(WIZARD_FINGER_OF_DEATH)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_FINGER_OF_DEATH)
END

IF
	See(Nearestenemyof(Myself))
	!See(SecondNearestEnemyOf(Myself))
	HaveSpell(WIZARD_FLAME_ARROW)
THEN
	RESPONSE #100
		Spell(Nearestenemyof(Myself),WIZARD_FLAME_ARROW)
END

IF
	See(ThirdNearestEnemyOf(Myself))
	HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
	See(ThirdNearestEnemyOf(Myself))
	HaveSpell(WIZARD_INCENDIARY_CLOUD)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_INCENDIARY_CLOUD)
END

IF
	See(ThirdNearestEnemyOf(Myself))
	HaveSpell(WIZARD_DELAYED_BLAST_FIREBALL)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_DELAYED_BLAST_FIREBALL)
END

IF
	HaveSpell(WIZARD_SUMMON_PLANATAR_EVIL)
	See(Nearestenemyof(Myself))
THEN
	RESPONSE #100
		Spell(Nearestenemyof(Myself),WIZARD_SUMMON_PLANATAR_EVIL)
END

IF
	HaveSpell(WIZARD_MORDENKAINENS_SWORD)
	See(Nearestenemyof(Myself))
THEN
	RESPONSE #100
		Spell(Nearestenemyof(Myself),WIZARD_MORDENKAINENS_SWORD)
END

IF
	See(Nearestenemyof(Myself))
	HaveSpell(WIZARD_MELF_METEOR)
	!HasItem("MELFMET",Myself)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_MELF_METEOR)
		AttackReevaluate(Nearestenemyof(Myself),30)
END

IF
	See(Nearestenemyof(Myself))
	!HaveSpell(WIZARD_MELF_METEOR)
	!HasItem("MELFMET",Myself)
	!CheckStatGT(LastSeenBy(Myself),0,RESISTFIRE)
	!CheckStatGT(LastSeenBy(Myself),0,RESISTMAGICFIRE)
	Global("Sequencer","LOCALS",0)
THEN
	RESPONSE #100
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_BURNING_HANDS)
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_BURNING_HANDS)
		DisplayString(Myself,@210646)
		SetGlobal("Sequencer","LOCALS",1)
END
