IF
	See(Nearestenemyof(Myself))
	Global("Sequencer","LOCALS",0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_BLUR)
		DisplayString(Myself, @25431)
		SetGlobal("Sequencer","LOCALS",1)
END


IF
	See(Nearestenemyof(Myself))
	HaveSpell(WIZARD_IMPROVED_INVISIBILITY)
	!StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_IMPROVED_INVISIBILITY)
END

IF
	HaveSpell(WIZARD_STONE_SKIN)
	CheckStatLT(Myself,1,STONESKINS)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_STONE_SKIN)
END

IF
	See(Nearestenemyof(Myself))
	Range(LastSeenBy(Myself),8)
	HaveSpell(WIZARD_FIRE_SHIELD_RED)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_FIRE_SHIELD_RED)
END

IF
	See(Nearestenemyof(Myself))
	OR(3)
		HPLT(LastSeenBy(Myself),40)
		Class(LastSeenBy(Myself),MAGE)
		Class(LastSeenBy(Myself),SORCERER)
	HaveSpell(WIZARD_FLAME_ARROW)
THEN
	RESPONSE #100
		Spell(Nearestenemyof(Myself),WIZARD_FLAME_ARROW)
END

IF
	OR(3)
		HitBy([ANYONE],CRUSHING)
		HitBy([ANYONE],SLASHING)
		HitBy([ANYONE],PIERCING)
		HaveSpell(WIZARD_SUN_FIRE)
		Range(LastAttackerOf(Myself),20)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SUN_FIRE)
END

IF
	See(SecondNearestEnemyOf(Myself))
	HaveSpell(WIZARD_WEB)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_WEB)
END

IF
	OR(2)
		!CheckStat(NearestEnemyOf(Myself),0,WEB)
		See(ThirdNearestEnemyOf(Myself))
	HaveSpell(WIZARD_FIREBALL)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_FIREBALL)
END

IF
	See(Nearestenemyof(Myself))
	HaveSpell(WIZARD_MELF_METEOR)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_MELF_METEOR)
END

IF
	See(Nearestenemyof(Myself))
	!HaveSpell(WIZARD_MELF_METEOR)
	HaveSpell(WIZARD_BURNING_HANDS)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),WIZARD_BURNING_HANDS)
END

IF
	See(Nearestenemyof(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(Nearestenemyof(Myself),30)
END